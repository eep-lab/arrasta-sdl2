{
  Stimulus Control
  Copyright (C) 2014-2023 Carlos Rafael Fernandes Pican√ßo.

  The present file is distributed under the terms of the GNU General Public License (GPL v3.0).

  You should have received a copy of the GNU General Public License
  along with this program. If not, see <http://www.gnu.org/licenses/>.
}
unit picanco.experiments.audio;

{$mode ObjFPC}{$H+}

interface

uses
  Classes, SysUtils, SpeechLib_TLB;

implementation

uses sdl.app.output, picanco.experiments.words.types, picanco.experiments.words;

type
  TPhonemeSet = (phIPA, phSYM);

function WordToSSML(const AWord : TWord;
  const APhonemeSet : TPhonemeSet = phIPA) : string;
begin
  Result :=
  '<speak version="1.0" xmlns="http://www.w3.org/2001/10/synthesis" xml:lang="pt-BR">' +
    '<voice name="Microsoft Maria Desktop - Portuguese(Brazil)">' +
      '<prosody rate="10%">';
      case APhonemeSet of
        phIPA : Result := Result +
          '<phoneme alphabet="ipa" ph="'+AWord.ToIPA+'">'+AWord.Caption+'</phoneme>';
        phSYM : Result := Result +
          '<PRON SYM="'+AWord.ToSYM+'"/>'+AWord.Caption;
      end;
  Result := Result +
      '</prosody>'+
    '</voice>'+
  '</speak>';
end;

procedure Speak(const AWord: TWord; const APhonemeSet: TPhonemeSet);
var
  LSpVoice: SpVoice;
begin
  LSpVoice := CoSpVoice.Create;
  try
    LSpVoice.Speak(WordToSSML(AWord, APhonemeSet), SVSFDefault);
  finally
    LSpVoice := nil;
  end;
end;

procedure SpeakToFile(const AWord: TWord;
  APhonemeSet: TPhonemeSet;
  AFileName: WideString);
var
  LSpFileStream: SpFileStream;
  LSpVoice: SpVoice;
begin
  LSpFileStream := CoSpFileStream.Create;
  LSpVoice := CoSpVoice.Create;
  try
    LSpFileStream.Format.Type_ := SAFT44kHz16BitStereo;
    LSpFileStream.Open(AFileName, SSFMCreateForWrite, False);
    LSpVoice.AudioOutputStream := LSpFileStream;
    LSpVoice.Speak(WordToSSML(AWord, APhonemeSet), SVSFDefault);
    LSpFileStream.Close;
  finally
    LSpVoice := nil;
    LSpFileStream := nil;
  end;
end;

procedure ListAvailableVoices;
var
  LSpVoice: SpVoice;
  TokenEnum: ISpeechObjectTokens;
  Token: ISpeechObjectToken;
  AttrValue: WideString;
  i: Integer;
begin
  LSpVoice := CoSpVoice.Create;
  try
    TokenEnum := LSpVoice.GetVoices('', '');
    for i := 0 to TokenEnum.Count-1 do
    begin
      Token := TokenEnum.Item(i);
      AttrValue := Token.GetDescription(0);
      Print(AttrValue);
    end;
  finally
    LSpVoice := nil;
  end;
end;

procedure Synthetize;
var
  LWord : TWord;
begin
  for LWord in Words do
    SpeakToFile(LWord, phIPA, LWord.Filenames.Audio+'.wav');
end;
//var i : integer;
initialization
  //ListAvailableVoices;
  //Synthetize;
  //for i := 0 to High(Words) do begin
  //  Speak(Words[i], phIPA);
  //end;


end.

